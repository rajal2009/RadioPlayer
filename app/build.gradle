plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.intertech.radioplayer"
        minSdkVersion 18
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    flavorDimensions "type"

    productFlavors {
        dev {
            applicationIdSuffix ".dev"
            versionCode 1
            versionName "1.0"
            // string variables
            resValue "string", "app_name", "RadioPlayer Dev"
            buildConfigField "String", "BASE_URL", "\"https://api.itmwpb.com/nowplaying/v3/935/\""
            // manifest variables
            manifestPlaceholders = [
                    appIcon     : "@mipmap/ic_launcher_alpha",
                    roundAppIcon: "@mipmap/ic_launcher_round_alpha"
            ]
        }
        stag {
            applicationIdSuffix ".stag"
            versionCode 1
            versionName "1.0.0"
            // string variables
            resValue "string", "app_name", "RadioPlayer Stg"
            buildConfigField "String", "BASE_URL", "\"https://api.itmwpb.com/nowplaying/v3/935/\""
            // manifest variables
            manifestPlaceholders = [
                    appIcon     : "@mipmap/ic_launcher_beta",
                    roundAppIcon: "@mipmap/ic_launcher_round_beta"
            ]
        }
        prod {
            versionCode 1
            versionName "1.0.0"
            // string variables
            resValue "string", "app_name", "RadioPlayer"
            buildConfigField "String", "BASE_URL", "\"https://api.itmwpb.com/nowplaying/v3/935/\""
            // manifest variables
            manifestPlaceholders = [
                    appIcon     : "@mipmap/ic_launcher",
                    roundAppIcon: "@mipmap/ic_launcher_round"
            ]
        }
    }

}

android.applicationVariants.all { variant ->
    variant.outputs.all { output ->
        def date = new Date().format('ddMMMyyyy')
        outputFileName = "RadioPlayer_" + variant.getFlavorName() + "_v" + variant.versionName + "_" + variant.versionCode + "_" + date + ".apk"
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'

    // view-model and live-data
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'

    // Android Navigation Architecture
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'

    // Local Unit Tests
    testImplementation "junit:junit:4.13.2"

    // Instrumented Unit Tests
    androidTestImplementation "junit:junit:4.13.2"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'

    // Retrofit and GSON
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation("com.squareup.okhttp3:logging-interceptor:4.9.1")

    // Kotlin Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0-RC'

    // hilt dependency injection
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // For progressbar
    implementation 'com.wang.avi:library:2.1.3'

    // Recyclerview
    implementation 'androidx.recyclerview:recyclerview:1.2.0'

    // glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    // Google ads Admob SDK
    implementation 'com.google.android.gms:play-services-ads:20.1.0'

    // exoplayer SDK
    implementation 'com.google.android.exoplayer:exoplayer-core:2.14.0'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.14.0'

    implementation 'com.android.support:multidex:1.0.3'
}